
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=armv6k-zlib
pkgver=1.2.11
pkgrel=1
pkgdesc='Deflate compression method library (for Nintendo 3DS homebrew development)'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("http://downloads.devkitpro.org/portlibs/sources/zlib-$pkgver.tar.xz")
sha256sums=('4ff941449631ace0d4d203e3483be9dbc9da454084111f97ea0a2114e19bf066')

build() {
  cd zlib-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/armv6kvars.sh

  CHOST=arm-none-eabi \
    ./configure --prefix="/opt/devkitpro/portlibs/armv6k" --static
  make libz.a # only build library, no programs
}

package() {
  cd zlib-$pkgver

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"/opt/devkitpro/portlibs/armv6k/licenses/$pkgname
  tail -n 31 README > "$pkgdir"/opt/devkitpro/portlibs/armv6k/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"/opt/devkitpro/portlibs/armv6k/share
}

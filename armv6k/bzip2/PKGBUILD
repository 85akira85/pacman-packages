
# Maintainer: Dave Murphy <davem@devkitpro.org>

pkgname=('armv6k-bzip2')
pkgver=1.0.6
pkgrel=2
pkgdesc="A high-quality data compression program"
arch=('any')
license=('custom')
url="http://www.bzip.org"
options=(!strip libtool staticlibs)
source=(http://www.bzip.org/$pkgver/bzip2-$pkgver.tar.gz)
sha256sums=('a2848f34fcd5d6cf47def00461fcb528a0484d8edef8208d6d2e2909dc61d9cd')


build() {

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/armv6kvars.sh

  make -C "$srcdir/bzip2-$pkgver" \
  CC=arm-none-eabi-gcc \
  AR=arm-none-eabi-ar \
  RANLIB=arm-none-eabi-ranlib \
  CPPFLAGS="${CPPFLAGS}" \
  CFLAGS="-D_FILE_OFFSET_BITS=64 -Winline ${CFLAGS}" libbz2.a

}

package() {
  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/armv6kvars.sh

  install -Dm 644 "$srcdir/bzip2-$pkgver"/LICENSE -t "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}"
  install -Dm 644 "$srcdir/bzip2-$pkgver"/bzlib.h -t "${pkgdir}/${PORTLIBS_PREFIX}/include/"
  install -Dm 644 "$srcdir/bzip2-$pkgver"/libbz2.a -t "${pkgdir}/${PORTLIBS_PREFIX}/lib"
}

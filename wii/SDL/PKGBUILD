# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Sergio Padrino <sergio.padrino@gmail.com>

pkgname=wii-sdl
pkgver=1.2.15
pkgrel=5
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Nintendo Wii port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('wii-pkg-config' 'devkitpro-pkgbuild-helpers')
source=("${url}/release/SDL-${pkgver}.tar.gz" "SDL-1.2.15.patch")
groups=('wii-portlibs' 'wii-sdl-libs')
sha256sums=(
  'd6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
  '5b90dd72695ffb9631f9ca838ced834b44e9e9377a88c5c5820bc5674e67be5b'
)

build() {
  cd SDL-$pkgver

  source /opt/devkitpro/wiivars.sh

  patch -p1 -i $srcdir/SDL-1.2.15.patch
  ./autogen.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static --enable-nintendo-wii
  make
}

package() {
  cd SDL-$pkgver

  source /opt/devkitpro/wiivars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Aaron Lindsay <aaron@aaronlindsay.com>

pkgname=switch-libpng
pkgver=1.6.34
pkgrel=1
pkgdesc='PNG format graphic files library (for Nintendo 3DS homebrew development)'
arch=('any')
url='http://www.libpng.org'
license=('custom')
options=(!strip libtool staticlibs)
depends=('switch-zlib')
makedepends=('devkitpro-pkgbuild-helpers')
source=("http://downloads.devkitpro.org/portlibs/sources/libpng-$pkgver.tar.xz")
sha256sums=('55952766b5db751efc7c5aa4157c6342783561b44040cfeef91961ab1dfca310')

build() {
  cd libpng-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="$DEVKITPRO/portlibs/switch" --host=aarch64-none-elf \
    --disable-shared --enable-static
  make
}

package() {
  cd libpng-$pkgver

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"/opt/devkitpro/portlibs/switch/share
}

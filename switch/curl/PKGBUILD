
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=switch-curl
pkgver=7.58.0
pkgrel=1
pkgdesc='Deflate compression method library (for Nintendo Switch homebrew development)'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
depends=('switch-zlib')
source=('https://github.com/curl/curl/releases/download/curl-7_58_0/curl-7.58.0.tar.xz'
)

sha256sums=( '6a813875243609eb75f37fa72044e4ad618b55ec15a4eafdac2df6a7e800e3e3'
)

build() {
  cd curl-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  ./configure --prefix=$PORTLIBS_PREFIX --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-ipv6 --disable-unix-sockets \
    --disable-manual --disable-ntlm-wb --disable-threaded-resolver

  make -C lib
  
}

package() {
  cd curl-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" -C lib install
  make DESTDIR="$pkgdir" -C include install

}
